	PROGRAM SECAO
	IMPLICIT NONE
	REAL,ALLOCATABLE:: X(:),Y(:)
	INTEGER I,K,N
	REAL XTEMP,YTEMP
	REAL HMIN,HMAX
	INTEGER CASO
	REAL DH,H,Y1,Y2,X1,X2,AP,PP,XC,YC
	REAL PT,AT,RAIO
	REAL PROFMED,BP,BT
	CHARACTER(50) FILEIN

	! ESTE PROGRAMA SERVE PARA CALCULAR ÁREA E RAIO HIDRÁULICO DE UMA SEÇÃO TRANSVERSAL
	! INICIA LENDO DADOS X, Y DA TABELA DE BATIMETRIA DA SECAO
	! IDENTIFICA COTA MAXIMA E MINIMA
	! CALCULA PROPRIEDADES DE ÁREA E RAIO HIDRÁULICO PARA DIFERENTES NÍVEIS DA ÁGUA
	! USA UM INCREMENTO ARBITRÁRIO DE FORMA A TER VINTE PONTOS NA TABELA FINAL

	!ABRE ARQUIVO DE ENTRADA
	OPEN(2,FILE='SECAO.SAI',STATUS='UNKNOWN',ACTION='WRITE')
	WRITE(2,*)'     NIVEL   LARGURA      AREA PERIMETRO    RAIO_H  PROF_MED'
    
    FILEIN='P2.ENT'
	!ABRE ARQUIVO DE ENTRADA
	!OPEN(1,FILE='SECAO.ENT',STATUS='OLD',ACTION='READ')
	OPEN(1,FILE=TRIM(FILEIN),STATUS='OLD',ACTION='READ')
	READ(1,*) ! LE CABEÇALHO X E Y
	I=0
	DO WHILE (.NOT. EOF(1))
		I=I+1
		READ(1,*)XTEMP,YTEMP
	ENDDO
	WRITE(*,*)'CHEGOU AO FIM DO ARQUIVO'
	N=I
	WRITE(*,*)'TABELA X,Y COM ',N,' PONTOS'
	!GRAVA DADOS DO ARQUIVO NOS VETORES
	ALLOCATE (X(N),Y(N))
	REWIND(1) !REINICIA LEITURA DO ARQUIVO
	READ(1,*)
	WRITE(*,*)'DADOS DE DISTANCIA X NIVEL (SECAO.ENT)'
	DO I=1,N
		READ(1,*)X(I),Y(I)
		WRITE(*,'(2F8.2)')X(I),Y(I)
	ENDDO
	CLOSE(1)
	!TERMINOU LEITURA DO ARQUIVO DE ENTRADA

	!DEFINE PARAMETROS PARA GERAR TABELA
	HMIN=MINVAL( Y )!IDENTIFICA MENOR COTA
	HMAX=MAXVAL( Y )!IDENTIFICA MAIOR COTA
	DH=(HMAX-HMIN)/100. !INCREMENTO DE COTA PARA TABELA
	H=HMIN+0.01 !começa com 1 centímetro de profundidade

	!LOOP DOS NÍVEIS D'ÁGUA
	DO
		IF(H>HMAX)EXIT

		BT=0.0
		AT=0.0
		PT=0.0
		!LOOP DOS PEDAÇOS DE SEÇÃO
		DO I=1,N-1
			Y1=Y(I)
			Y2=Y(I+1)
			X1=X(I)
			X2=X(I+1)
			!DEFINE QUAL É O CASO
			IF(Y1<H)THEN
				IF(Y2<H)THEN
					CASO=1 !NÍVEL DA ÁGUA É MAIOR NAS DUAS COORDENADAS
				ELSE
					CASO=3 !NÍVEL DA ÁGUA É MAIOR NA PRIMEIRA COORDENADA E MENOR OU IGUAL NA SEGUNDA
				ENDIF
			ELSEIF(Y1>=H)THEN
				IF(Y2<H)THEN
					CASO=2 !NÍVEL DA ÁGUA É MENOR OU IGUAL NA PRIMEIRA COORDENADA MAIOR NA SEGUNDA
				ELSE
					CASO=4 !NÍVEL DA ÁGUA MENOR NAS DUAS COORDENADAS
				ENDIF
			ENDIF
			!CASO DEFINIDO


			SELEC_CASO: SELECT CASE  (CASO)
				CASE(1)
					AP=(H-Y1+H-Y2)*(X2-X1)*0.5 !ÁREA PARCIAL
					PP=((X2-X1)**2.+(Y2-Y1)**2.)**0.5 !PERÍMETRO PARCIAL
					BP=X2-X1 !LARGURA PARCIAL
				CASE(2)
					YC=H !COORDENADA Y DO CRUZAMENTO DAS RETAS
					XC=X1+(X2-X1)*(YC-Y1)/(Y2-Y1) !X DO CRUZAMENTO DAS RETAS
					AP=(H-Y2)*(X2-XC)*0.5 !ÁREA PARCIAL
					PP=((X2-XC)**2.+(Y2-YC)**2.)**0.5 !PERÍMETRO PARCIAL
					BP=X2-XC !LARGURA PARCIAL
				CASE(3)
					YC=H !COORDENADA Y DO CRUZAMENTO DAS RETAS
					XC=X1+(X2-X1)*(YC-Y1)/(Y2-Y1) !X DO CRUZAMENTO DAS RETAS
					AP=(H-Y1)*(XC-X1)*0.5 !ÁREA PARCIAL
					PP=((XC-X1)**2.+(YC-Y1)**2.)**0.5 !PERÍMETRO PARCIAL
					BP=XC-X1 !LARGURA PARCIAL
				CASE(4)
					AP=0.0
					PP=0.0
					BP=0.0
			END SELECT SELEC_CASO

			AT=AT+AP !ÁREA TOTAL
			PT=PT+PP !PERÍMETRO TOTAL
			BT=BT+BP !LARGURA SUPERFICIAL TOTAL

		ENDDO
		RAIO=AT/PT
		PROFMED=AT/BT
		
		
		WRITE(2,'(6F10.2)')H,BT,AT,PT,RAIO,PROFMED

		H=H+DH
	ENDDO

	CLOSE(2)
    
    !SE QUISER INTERPOLAR UMA SECAO QQ
	!CALL LESECAO
	
	END PROGRAM