!**************
!PROGRAMA DE PROPAGAÇÃO DE UM HIDROGRAMA PELO MÉTODO DE BATES UNIDIMENSIONAL
!FERNANDO MAINARDI FAN
!INSTITUTO DE PESQUISAS HIDRÁULICAS - NOV/2012
!**************

!CHAMAMOS O PROGRAMA
PROGRAM PROPAGACAO_BATES

!DECLARO QUE NÃO POSSO TER VARIÁVEIS IMPLICITAS NO FORTRAN
IMPLICIT NONE

!---VARIÁVEIS DO PROGRAMA
!VARIÁVEIS DO TIPO VETOR
REAL QOBS(10000) !VAZÕES LIDAS NO ARQUIVO DE ENTRADA
REAL QSAI(10000) !VAZÕES DE SAÍDA EM FORMATO HORÁRIO
REAL HJUS(10000) !NÍVEIS DE ÁGUA NA CONDIÇÃO DE CONTORNO DE JUSANTE
REAL,ALLOCATABLE:: Q(:,:) !VAZÕES UTILIZADAS NO MODELO DE PROPAGAÇÃO
REAL,ALLOCATABLE:: QBATES(:,:) !VAZÃO DIVIDIDA PELA LARGURA UTILIZADA NO MODELO DE PROPAGAÇÃO
REAL,ALLOCATABLE:: V(:,:) !VELOCIDADE DA ÁGUA EM M/S
REAL,ALLOCATABLE:: HAGUAi(:,:) !ALTURA DA LÂMINA DE ÁGUA EM CADA CÉLULA UTILIZADA NO MODELO DE PROPAGAÇÃO
REAL,ALLOCATABLE:: Zi(:,:) !COTA EM RELAÇÃO AO NÍVEL DO MAR DE CADA CÉLULA UTILIZADA NO MODELO DE PROPAGAÇÃO
REAL,ALLOCATABLE:: Yi(:,:) !ELEVAÇÃO DA SUPERFICÍES DE ÁGUA (SOMA Zi + HAGUAi) DE CADA CÉLULA UTILIZADA NO MODELO DE PROPAGAÇÃO
REAL,ALLOCATABLE:: HF(:,:) !COEFICIENTE NÃO LINEAR DO MODELO DE PROPAGAÇÃO CALCULADO POR {MAX[Yi(N,IT-1),Yi(N-1,IT-1)] - MAX[Zi(N,IT-1),Zi(N-1,IT-1)]}
!VARIÁVEIS SIMPLES   
INTEGER NTOBS !NÚMERO DE INTERVALOS DE TEMPO NO ARQUIVO DE ENTRADA
INTEGER IT !INTERVALO DE TEMPO
INTEGER NT !NÚMERO DE INTERVALOS DE TEMPO UTILIZADO NO MODELO
INTEGER NTHORA !NÚMERO DE INTERVALOS DE TEMPO EM UMA HORA
INTEGER I,J,K,L !VARIÁVEIS INTEIRAS PARA AUXÍLIO LEITURA
INTEGER T, TT !VARIÁVEL PARA CÁLCULO DOS TEMPOS
INTEGER NN !VARIÁVEL PARA CÁLCULO DOS TRECHOS
REAL DT !PASSO DE TEMPO DO MODELO EM SEGUNDOS
INTEGER NX !NÚMERO DE TRECHOS DE RIO EM QUE O TRECHO É DIVIDIDO
REAL DX !PASSO DE ESPAÇO DO MODELO EM METROS
INTEGER LTRECHO !COMPRIMENTO DO TRECHO DE RIO EM METROS
REAL DT_DX !VALOR DA DIVISÃO DT/DX
REAL COURANT !CONDIÇÃO DE COURANT-FREIDRICHS-LEVY
REAL ZREF !COTA DE REFERÊNCIA EM RELAÇÃO AO NÍVEL DO MAR EM METROS
REAL B !LARGURA DO RIO EM METROS
REAL SO !DECLIVIDADE DO TRECHO DE RIO EM M/M
REAL NMAN, NMANQD !NÚMERO DO COEFICEINTE DE RUGOSIDADE MANNING PARA O TRECHO DE RIO - NORMAL E AO QUADRADO
REAL GRAVIDADE !ACELERAÇÃO DA GRAVIDADE EM M/S²
REAL TETA !TETA DO ESQUEMA DE ALMEIDA
REAL ALFA, BETA, HINI, AINI !VARIÁVEIS UTILIZADAS PARA FAZER A ESTIMATIVA DO HAGUAi INICIAL EM IT =1 E HAGUA DE JUSANTE
REAL SIGMA, FT, FR, M !PARÂMETRO SIGMA, LIMIAR DO NÚMERO DE FROUDE, NÚMERO DE FROUDE, PARÂMETRO M: VARIÁVEIS DO MÉTODO PARA CONTROLE DE NÚMERO DE FROUDE PELA TÉCNICA LPI (FREAD, 1996)
REAL SIGMALIM !MENOR SIGMA NOS DADOS DE ENTRADA
REAL FR_ULT !ÚLTIMO FR CALCULADO
!ENCERRA DECLARAÇÃO DAS VARIÁVEIS
!--------------------------------------------

!---ATRIBUI OS VALORES ÀS VARIÁVEIS
!NÚMERO DE INTERVALOS DE TEMPO NO ARQUIVO DE ENTRADA EM HORAS
NTOBS = 360.0 !7189.0
!PASSO DE TEMPO EM SEGUNDOS
DT = 60.0
!NÚMERO DE INTERVALOS DE TEMPO DENTRO DE UMA HORA (3600s)
NTHORA = 3600.0/DT
!NÚMERO TOTAL DE INTERVALOS DE TEMPO A SER UTILIZADO NO MODELO (NÚMERO DE INTERVALOS EM UMA HORA, VEZES O NÚMERO DE HORAS MENOS 1)
NT = (NTOBS-1)*NTHORA
!PASSO DE ESPAÇO EM METROS
DX = 1000.0
!COMPRIMENTO TOTAL DO TRECHO DE RIO SIMULADO EM METROS
LTRECHO = 50000.0
!NÚMERO DE TRECHOS DE RIO
NX = LTRECHO/DX + 1 !MAIS UM, POIS CALCULAMOS A PROPAGAÇÃO ATÉ A ENTRADA DO ÚLTIMO TRECHO
!LARGURA DO RIO EM METROS
B = 30.0
!DECLIVIDADE DO TRECHO DE RIO EM M/M 
SO = 0.0001
!ACELERAÇÃO DA GRAVIDADE EM M/S²
GRAVIDADE = 9.806
!NÚMERO DO COEFICEINTE DE RUGOSIDADE DE MANNING PARA O TRECHO DE RIO 
NMAN = 0.035
!NÚMERO DO COEFICEINTE DE RUGOSIDADE DE MANNING AO QUADRADO
NMANQD = NMAN**2
!COTA Z DE REFERÊNCIA EM METROS
ZREF = 100.0
!DEFINE A PONDERAÇÃO DO ESQUEMA DE ALMEIDA
TETA = 0.5
!VALOR DA DIVISÃO DT/DX
DT_DX = DT/DX
!ALOCA ESPAÇO NAS VARIÁVEIS DE VAZÃO, VAZÃObates, HAGUA, Z, Y E HF 
ALLOCATE (Q(NX,NT)) !VAZÃO EM TODOS OS TRESCHOS EM TODOS OS INTERVALOS DE TEMPO
ALLOCATE (QBATES(NX,NT)) !VAZÃO DIVIDIDA PELA LARGURA UTILIZADA NO MODELO DE PROPAGAÇÃOS EM TODOS OS INTERVALOS DE TEMPO
ALLOCATE (HAGUAi(NX,NT)) !ALTURA DA LÂMINA DE ÁGUA EM CADA CÉLULA UTILIZADA NO MODELO DE PROPAGAÇÃO EM TODOS OS TRESCHOS EM TODOS OS INTERVALOS DE TEMPO
ALLOCATE (Zi(NX,NT)) !COTA EM RELAÇÃO AO NÍVEL DO MAR DE CADA CÉLULA UTILIZADA NO MODELO DE PROPAGAÇÃO EM TODOS OS TRESCHOS EM TODOS OS INTERVALOS DE TEMPO
ALLOCATE (Yi(NX,NT)) !ELEVAÇÃO DA SUPERFICÍES DE ÁGUA (SOMA Zi + HAGUAi) DE CADA CÉLULA UTILIZADA NO MODELO DE PROPAGAÇÃO EM TODOS OS TRESCHOS EM TODOS OS INTERVALOS DE TEMPO
ALLOCATE (HF(NX,NT)) !COEFICIENTE NÃO LINEAR DO MODELO DE PROPAGAÇÃO CALCULADO POR {MAX[Yi(N,IT-1),Yi(N-1,IT-1)] - MAX[Zi(N,IT-1),Zi(N-1,IT-1)]} EM TODOS OS TRESCHOS EM TODOS OS INTERVALOS DE TEMPO
ALLOCATE (V(NX,NT)) !VELOCIDADE DA ÁGUA EM TODOS OS TRECHOS E TODOS OS INTERVALOS
!CALCULO DOS PARAMETROS DA ALTURA DE ÁGUA INICIAL DO MODELO
ALFA = (NMAN*(B**(2.0/3.0)))**0.6/((SO**0.5)**0.6) !PARAMETRO ALFA DA FÓRMULA ALFA*VAZAO^BETA
BETA = 0.6 !PARAMETRO BETA DA FÓRMULA ALFA*VAZAO^BETA
!ENCERRA ATRIBUIÇÃO DOS VALORES
!--------------------------------------------

!---EXECUTA O PROGRAMA

!ESCREVE CABEÇALHO DO PROGRAMA NA TELA
WRITE(*,*) 'PROGRAMA DE PROPAGACAO UNIDIMENSIONAL PELO MODELO DE BATES'
WRITE(*,*) 'FERNANDO MAINARDI FAN'
WRITE(*,*) 'INSTITUTO DE PESQUISAS HIDRAULICAS - NOV/2012'
WRITE(*,*)

!ESCREVE INFORMAÇÕES DO TRECHO NA TELA
WRITE(*,*)'NUMERO DE INTERVALOS DE TEMPO NO ARQUIVO DE ENTRADA EM HORAS= ', NTOBS
WRITE(*,*)'DT= ',DT
WRITE(*,*)'NUMERO DE INTERVALOS DE TEMPO DENTRO DE UMA HORA= ',NTHORA
WRITE(*,*)'NUMERO DE INTERVALOS DE TEMPO A SER UTILIZADO NO MODELO= ', NT
WRITE(*,*)'DX= ',DX
WRITE(*,*)'COMPRIMENTO DO TRECHO DE RIO EM METROS= ',LTRECHO
WRITE(*,*)'LARGURA DO RIO EM METROS= ',B
WRITE(*,*)'DECLIVIDADE DO TRECHO DE RIO EM M/M= ',SO
WRITE(*,*)'ACELERACAO DA GRAVIDADE EM M2/S= ',GRAVIDADE
WRITE(*,*)'COEFICIENTE DE RUGOSIDADE DE MANNING= ',NMAN
WRITE(*,*)'VALOR DA DIVISAO DT/DX= ',DT_DX
WRITE(*,*)'ALFA E BETA= ',ALFA, BETA
PAUSE

!FAZ A LEITURA DO ARQUIVO DE ENTRADA DE VAZAO
!ESCREVE A AÇÃO NA TELA
WRITE(*,*) 'LENDO O ARQUIVO DE ENTRADA DE VAZOES...'

!ABRE O ARQUIVO DE VAZÕES DE ENTRADA
OPEN(1,FILE='ENTRADA.txt',STATUS='OLD',ACTION='READ')

!FAZ A LEITURA DE TODO O ARQUIVO
DO T=1,NTOBS
    !FAZ A LEITURA DE CADA LINHA DO ARQUIVO EM FORMATO LIVRE
	READ(1,*)(QOBS(T))	!ARQUIVO COM UMA COLUNA
	!READ(1,*)I,(QOBS(T))	!ARQUIVO COM DUAS COLUNAS
	!WRITE(*,*) QOBS(T)
ENDDO

!FECHA O ARQUIVO DE ENTRADA
CLOSE (1)

!ABRE O ARQUIVO DE CONDIÇÃO DE CONTORNO DE JUSANTE COM A MARÉ
OPEN(1,FILE='CC_JUSANTE.txt',STATUS='OLD',ACTION='READ')

!FAZ A LEITURA DE TODO O ARQUIVO
DO T=1,NTOBS
    !FAZ A LEITURA DE CADA LINHA DO ARQUIVO EM FORMATO LIVRE
	READ(1,*)(HJUS(T))
ENDDO

!FECHA O ARQUIVO DE ENTRADA
CLOSE (1)

!AVISA QUE TERMINOU A LEITURA
WRITE(*,*) 'LEITURA DOS ARQUIVOS DE ENTRADA FINALIZADA'

!INTERPOLA OS DADOS DE VAZAO DE HORÁRIO PARA DT USANDO A FINT E SALVA NA VARIÁVEL DE VAZÃO DE CONTORNO DO MODELO Q(1,IT)

!ZERA A VARIÁVEL DE CONTADOR DE INTERVALO DE TEMPO
IT=0
!COMEÇA O LOOP DE INTERPOLAÇÃO
DO T=1,(NTOBS-1) !PARA CADA PASSO DE TEMPO NA VAZÃO OBSERVADA
    DO TT=0,(NTHORA-1) !PERCORRE O NÚMERO INTERVALOS DE TEMPO DENTRO DELE
    !SOMA UM INTERVALO DE TEMPO NO IT
    IT = IT + 1
    !ATRIBUI O VALOR INTERPOLADO À VARIÁVEL DE VAZÃO
    Q(1,IT)= QOBS(T)+(QOBS(T+1)-QOBS(T))*TT/NTHORA
    !CALCULA A VAZÃO DIVIDIDA PELA LARGURA DA SEÇÃO
    QBATES(1,IT)= Q(1,IT)/B
    !ESCREVE O RESULTADO NA TELA
    !WRITE(*,*)IT,Q(1,IT)
    ENDDO !VAI PARA O PRÓXIMO DT
ENDDO !VAI PARA PRÓXIMA HORA OBSERVADA
!FIM DO LOOP DE INTERPOLAÇÃO

!CALCULA A CONDIÇÃO INICIAL DO MODELO

!ESCREVE A AÇÃO NA TELA
WRITE(*,*) 'CALCULANDO CONDICOES INICIAIS...'

!FAZ UM LOOP QUE PERCORRE TODOS OS TRECHOS DE RIO EM T=1
DO NN=1,NX
    !PARA CADA TRECHO CALCULA A VAZÃO INICIAL COMO SENDO IGUAL À VAZÃO EM T=1 DO PRIMEIRO TRECHO
    Q(NN,1)= Q(1,1)
    !CALCULA A QBATES
    QBATES(NN,1)=Q(1,1)/B
    !ESTIMA A ALTURA DA LÂMINA DE ÁGUA UTILIZANDO UMA RELAÇÃO SIMPLIFICADA
    HAGUAi(NN,1)=ALFA*(Q(1,1)**BETA)/B + 0.00001
    !CALCULA A COTA DE CADA CÉLULA
    Zi(NN,1)=ZREF-(NN-1)*DX*SO
    !CALCULA A ELEVAÇÃO DA SUPERFICÍES DE ÁGUA (SOMA Zi + HAGUAi) DE CADA CÉLULA
    Yi(NN,1) = Zi(NN,1) + HAGUAi(NN,1)
    !CALCULA O HF INICIAL DO MODELO = {MAX[Yi(N,IT),Yi(N-1,IT)] - MAX[Zi(N,IT),Zi(N-1,IT)]}
    IF (NN>1) THEN !NÃO CALCULA O HF DA PRIMEIRA CÉLULA
        HF(NN,1) = MAX(Yi(NN-1,1),Yi(NN,1))-MAX(Zi(NN-1,1),Zi(NN,1))
    ELSE
        HF(NN,1)=0.0
    ENDIF
    !CALCULA A VELOCIDADE
    V(NN,1)= QBATES(NN,1)/HAGUAi(NN,1)
    !ESCREVE O RESULTADO NA TELA
    !WRITE(*,*)NN,Q(NN,1),QBATES(NN,1),HAGUAi(NN,1),Zi(NN,1),Yi(NN,1),HF(NN,1)
ENDDO
!FINALIZA O LOOP DE CONDIÇÕES INICIAIS

!FAZ UM LOOP QUE DEFINE A CONDIÇÃO DE CONTORNO DE JUSANTE

!ESCREVE A AÇÃO NA TELA
WRITE(*,*) 'CALCULANDO CONDICOES DE CONTORNO DE JUSANTE...'

!ZERA A VARIÁVEL DE CONTADOR DE INTERVALO DE TEMPO
IT=0
!COMEÇA O LOOP DE INTERPOLAÇÃO
DO T=1,(NTOBS-1) !PARA CADA PASSO DE TEMPO NO NÍVEL OBSERVADO
    DO TT=0,(NTHORA-1) !PERCORRE O NÚMERO INTERVALOS DE TEMPO DENTRO DELE
    !SOMA UM INTERVALO DE TEMPO NO IT
    IT = IT + 1
    !ATRIBUI O VALOR INTERPOLADO À VARIÁVEL DE CONDIÇÃO DE JUSANTE
    HAGUAi(NX,IT)= HJUS(T)+(HJUS(T+1)-HJUS(T))*TT/NTHORA
    Zi(NX,IT)=Zi(NX,1) !COTA
    Yi(NX,IT) = HAGUAi(NX,IT)+Zi(NX,IT) !ELEVAÇÃO DA SUPERFICÍES DE ÁGUA (SOMA Zi + HAGUAi)
    ENDDO !VAI PARA O PRÓXIMO DT
ENDDO !VAI PARA PRÓXIMA HORA OBSERVADA
!FIM DO LOOP DE INTERPOLAÇÃO
!DETERMINOU CONDIÇÃO DE CONTORNO DE JUSANTE

!CALCULA A CONDIÇÃO DE COURANT-FREIDRICHS-LEVY
COURANT = DT_DX*(GRAVIDADE*HAGUAi(1,1))**0.5
WRITE(*,*)'CONDICAO DE COURANT-FREIDRICHS-LEVY= ',COURANT
PAUSE

!ESCREVE AÇÃO DE COMEÇAR SIMULAÇÃO
WRITE(*,*)'SIMULANDO...'

!ENTRA NO MÉTODO DE PROPAGAÇÃO
DO IT=2,NT !PARA CADA INTERVALO DE TEMPO
    DO NN=1,NX-1 !PARA CADA CÉLULA
    
       !CALCULA O NÚMERO DE FROUDE=
       FR=V(NN+1,IT-1)/((GRAVIDADE*HAGUAi(NN,IT-1))**0.5)
               
        !CALCULA HFi PARA A PRÓXIMA CÉLULA
        HF(NN+1,IT) = MAX(Yi(NN+1,IT-1),Yi(NN,IT-1))-MAX(Zi(NN+1,IT-1),Zi(NN,IT-1))
        
        !CALCULA A VAZÃO
        QBATES(NN+1,IT)=((TETA*QBATES(NN+1,IT-1)+((1-TETA)/2)*(QBATES(NN,IT-1)+QBATES(NN+1,IT-1)))-GRAVIDADE*HF(NN+1,IT)*DT_DX*(Yi(NN+1,IT-1)-Yi(NN,IT-1)))/(1.0+GRAVIDADE*DT*NMANQD*ABS(QBATES(NN+1,IT-1))/(HF(NN+1,IT)**(7.0/3.0))) 
         
        !CALCULA O VALOR DA VAZÃO PARA A PRÓXIMA CÉLULA
        Q(NN+1,IT)=QBATES(NN+1,IT)*B
        
        !CALCULA O VALOR DE DE HÁGUAi FAZENDO UM BALANÇO ENTRE AS VAZÕES DE ENTRADA E DE SAÍDA
        HAGUAi(NN,IT)= ((QBATES(NN,IT)-QBATES(NN+1,IT))/DX)*DT + HAGUAi(NN,IT-1)
        
        IF(HAGUAi(NN,IT)<0.001)THEN
            WRITE(*,*)HAGUAi(NN,IT)
        ENDIF
        
        !CALCULA O VALOR DE Z COMO SENDO IGUAL A Z DO INTERVALO DE TEMPO ANTERIOR
        Zi(NN,IT)= Zi(NN,IT-1)
        !CALCULA A ELEVAÇÃO DA SUPERFICÍES DE ÁGUA (SOMA Zi + HAGUAi)
        Yi(NN,IT) = Zi(NN,IT) + HAGUAi(NN,IT)
        
        !CALCULA A VELOCIDADE COM QUE A ÁGUA PASSA NA CÉLULA
        V(NN+1,IT)= QBATES(NN+1,IT)/ ((HAGUAi(NN,IT)+HAGUAi(NN+1,IT-1))/2)

    ENDDO         
    !ESCREVE NA TELA
   ! WRITE(*,*)Q(1,IT-1),QBATES(1,IT-1),HF(1,IT-1),HAGUAi(1,IT-1),Zi(1,IT-1),Yi(1,IT-1),Q(2,IT-1),QBATES(2,IT-1),HF(2,IT-1),HAGUAi(2,IT-1),Zi(2,IT-1),Yi(2,IT-1)
    !PAUSE
ENDDO
!FINALIZA O LOOP DE PROPAGAÇÃO

!ESCREVE A AÇÃO NA TELA
WRITE(*,*) 'PROPAGACAO FINALIZADA!'

!FAZ A LEITURA DO ARQUIVO DE ENTRADA DE VAZAO
!ESCREVE A AÇÃO NA TELA
WRITE(*,*) 'GERANDO ARQUIVO DE SAIDA DE VAZOES...'

!ABRE O ARQUIVO DE VAZÕES DE SAÍDA
OPEN(2,FILE='SAÍDA.TXT',STATUS='UNKNOWN',ACTION='WRITE')

!ESCREVE O CABEÇALHO
!WRITE(2,*)'         IT    ENTRADA          SAÍDA          VELOCIDADE' !DESLIGADO PARA TESTES ARTIGO
WRITE(2,*)'         IT    ENTRADA          SAÍDA          PROFUNDIDADESAIDA' !DESLIGADO PARA TESTES ARTIGO

!ESCREVE O ARQUIVO
DO IT=1,NT
    !ESCREVE TODAS AS VAZÕES EM CADA LINHA
	!WRITE(2,*)IT,Q(1,IT),Q(NX,IT),V(NX,IT) !DESLIGADO PARA TESTES ARTIGO
	WRITE(2,*)IT,Q(1,IT),Q(NX-1,IT),HAGUAi(NX-2,IT)!DESLIGADO PARA TESTES ARTIGO
ENDDO

!FECHA O ARQUIVO DE SAÍDA
CLOSE (2)

!ABRE O ARQUIVO DE VAZÕES DE SAÍDA SOMENTE COM UMA COLUNA
OPEN(4,FILE='SAÍDA_COLUNA.TXT',STATUS='UNKNOWN',ACTION='WRITE')

!ESCREVE O CABEÇALHO
!WRITE(2,*)'         IT    ENTRADA          SAÍDA          VELOCIDADE' !DESLIGADO PARA TESTES ARTIGO

!ESCREVE O ARQUIVO
DO IT=1,NT
    !ESCREVE TODAS AS VAZÕES EM CADA LINHA
	!WRITE(2,*)IT,Q(1,IT),Q(NX,IT),V(NX,IT) !DESLIGADO PARA TESTES ARTIGO
	WRITE(4,*)Q(NX-1,IT)
ENDDO

!FECHA O ARQUIVO DE SAÍDA
CLOSE (4)

!INTERPOLA OS DADOS DE VAZAO DE DT PARA HORÁRIO NOVAMENTE
!ZERA A VARIÁVEL DE CONTADOR DE INTERVALO DE TEMPO
IT=0
!COMEÇA O LOOP DE INTERPOLAÇÃO
DO T=1,(NTOBS-1) !PARA CADA PASSO DE TEMPO NA VAZÃO OBSERVADA
    QSAI(T) = 0 !ZERA A VARIÁVEL DE VAZÃO DE SAÍDA
    DO TT=0,(NTHORA-1) !PERCORRE O NÚMERO INTERVALOS DE TEMPO DENTRO DELE
    !SOMA UM INTERVALO DE TEMPO NO IT
    IT = IT + 1
    !SOMA A VAZÃO
    QSAI(T) = QSAI(T) + Q(NX,IT)
    ENDDO !VAI PARA O PRÓXIMO DT
    QSAI(T)=QSAI(T)/NTHORA !CALCULA QSAI COMO SENDO A MÉDIA
ENDDO !VAI PARA PRÓXIMA HORA OBSERVADA
QSAI(NTOBS)=Q(NX,NT)
!FIM DO LOOP DE INTERPOLAÇÃO

!ABRE O ARQUIVO DE VAZÕES DE SAÍDA HORÁRIO
OPEN(3,FILE='SAÍDA_HORA.TXT',STATUS='UNKNOWN',ACTION='WRITE')
!ESCREVE O CABEÇALHO
WRITE(3,*)'          T          SAÍDA'
!ESCREVE O ARQUIVO
DO T=1,NTOBS
    !ESCREVE TODAS AS VAZÕES EM CADA LINHA
	WRITE(3,*)T,QSAI(T)
ENDDO

!FECHA O ARQUIVO DE SAÍDA
CLOSE (3)

!ABRE O ARQUIVO DE NÍVEIS
OPEN(5,FILE='SAÍDA_NÍVEIS.TXT',STATUS='UNKNOWN',ACTION='WRITE')
WRITE(5,72)(Zi(NN,1),NN=1,NX) !ESCREVE OS NIVEIS DE AGUA

!ESCREVE O ARQUIVO
DO IT=1,NT
    WRITE(5,72)(Yi(NN,IT),NN=1,NX) !ESCREVE OS NIVEIS DE AGUA
ENDDO

!FECHA O ARQUIVO DE SAÍDA
CLOSE (5)

!ENCERRA O PROGRAMA
WRITE(*,*)'PROGRAMA TERMINOU'
PAUSE

72	FORMAT(<NX>F10.2)
END PROGRAM PROPAGACAO_BATES
!--------------------------------------------